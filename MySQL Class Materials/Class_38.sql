---------------Enable file stream at instance level-----------------
EXEC sp_configure filestream_access_level, 2;
GO
RECONFIGURE;
GO

EXEC sp_configure filestream_access_level;
GO

-------------Create a filestream enabled file group -----------------
ALTER DATABASE SalesDB 
ADD FILEGROUP FileStreamFileGroup1 CONTAINS FILESTREAM;
GO

--------------Create a file stream object location ------------------
ALTER DATABASE SalesDB 
	ADD FILE (NAME = FileStreamFileGroup1_File1,FILENAME = 'C:\SalesDB\FSDATA')
		TO FILEGROUP FileStreamFileGroup1;
GO

-------------Create a file stream enabled table ---------------------
CREATE TABLE DocumentStore 
(
	DocumentID INT IDENTITY PRIMARY KEY,
	Document VARBINARY (MAX) FILESTREAM NULL,
	DocGUID UNIQUEIDENTIFIER NOT NULL ROWGUIDCOL UNIQUE DEFAULT NEWID (),
	[DateTime] DATETIME DEFAULT GETDATE()
)
ON [PRIMARY]
FILESTREAM_ON FileStreamFileGroup1;
GO

--------------------
INSERT INTO [DocumentStore] (Document)
SELECT * FROM 
OPENROWSET(BULK N'C:\MyFiles\Philadelphia_Eagles.JPG' ,SINGLE_BLOB) AS Document
GO

select * from DocumentStore

------------------------Data Masking------------------------------
--Creating a Table That Contains Masked Data

CREATE TABLE ClientInfo
(
   ClientID int IDENTITY,
   FirstName varchar(65),
   LastName varchar(65),
   PhoneNum bigint  MASKED WITH (FUNCTION = 'default()'),
   EmailAddr varchar(100) MASKED WITH (FUNCTION = 'email()'),
   CreditCardNum varchar(19) MASKED WITH (FUNCTION = 'partial(0,"XXXX-XXXX-XXXX-",4)'),
   BirthDT date MASKED WITH (FUNCTION = 'default()')
);

INSERT Clientinfo (FirstName, LastName, PhoneNum, EmailAddr,CreditCardNum,BirthDT) 
VALUES 
('George', 'Washington', 5555814441, 'GeorgeW@datanbasejournal.com', '0123-4567-8901-2345','02/22/1732'),
('Thomas', 'Jefferson', 5559841298, 'ThomasJ@datanbasejournal.com', '9999-9999-9999-9999', '04/13/1743'),
('Abraham', 'Lincoln', 5554070123, 'AbrahamL@datanbasejournal.com','0000-1111-2222-3333', '02/12/1809');

select * from ClientInfo

revoke UNMASK TO LoginLisa;

------------------------STRING_SPLIT----------------------
--STRING_SPLIT is a inline multi statement function built in to SQL Server 2016

SELECT * 
FROM STRING_SPLIT('The lousy DBA splits strings with WHILE loops', ' ')

--------------------Always Encrypted---------------------
CREATE DATABASE AEDemo;

--Create a column master key
USE [AEDemo]
CREATE COLUMN MASTER KEY [Demo_Always_Encrypted_CMK]
WITH
(
	KEY_STORE_PROVIDER_NAME = N'MSSQL_CERTIFICATE_STORE',
	KEY_PATH = N'CurrentUser/My/BC0BE7A07EF74B34D04471C5442076A6DE7DB8E4'
)
GO

--Create a column encryption key
CREATE COLUMN ENCRYPTION KEY [Demo_Always_Encrypted_CEK]
WITH VALUES
(
	COLUMN_MASTER_KEY = [Demo_Always_Encrypted_CMK],
	ALGORITHM = 'RSA_OAEP',
	ENCRYPTED_VALUE = 0x016E000001630075007200720065006E00740075007300650072002F006D0079002F00620063003000620065003700610030003700650066003700340062003300340064003000340034003700310063003500340034003200300037003600610036006400650037006400620038006500340098CA0CCA52F19C623DBE40166EEDD3FE1A200D4237C9D1C8F2E6F1DBE4B0CA086107F0BC800F8E369E711AB38709CC003231B2A41D5C1BA01700983B0CDDB6406D188D7EED97D0FD137422363E3F246C72B1AE43B3A9B02BF3C09D18F7CD26F29F4E09B3D3E26CF6EF5ECE43E805F3305B0D3E32484D00BFA50AF79E40E0315740F8F38D7A9F3D4CC6B7F296FD291F673DB6D326AB843C97B27400DD5B58444054E5692068DA7520B7DAF6E26CE9E8B3F2A018106BAFEE13A20784FAE6E793F22AA0DC150135FD0962EDD8F0A99FBDE4EED012D1E2AD9AC10B724A47E624E1A21EB7F54DA8E862DF739F21EE68DBC60CD90FE1566D934EB640221A6CF47AC28A7413757B76EBFD4C213C8B1A44833F8ED81B055069F2D0308EB9107021BABA4744D932A30FA36E4CA0AF15938501A4F4AEE47480432E6BD83349FEA4574FEF58C2FA8A93DCED598B2DBE806130BB5D5A0FB89857C4B2E212ACEB62EAB61BDBAF20F633B5503F310AE3AF4C9649769EC0D7D044A47410964B679DC10656BD4A9AEA6891ED9E51CFBE07341E2C946F6778D2F41771593561BFC44AAE414ECA0E23A11C538C5D86B4DA425784833049B10C7C8E3DFD57965BB7B45715940684CE4AACA49253FB4038BAAEEABA60E7B08420DB5157A00FE510F260F558F48B43BEB54C1BCE7855D063F83DA2D0C896C1C923008A96FB81CB9BE4B70F625F2BFC816B
)
GO

--Create a Always Encrypted table 
CREATE TABLE dbo.Demo_Always_Encrypted 
(
   ID INT IDENTITY(1,1) PRIMARY KEY,
   LastName NVARCHAR(45),
   FirstName NVARCHAR(45),
   BirthDate DATE 
		ENCRYPTED WITH 
		(
			ENCRYPTION_TYPE = RANDOMIZED, 
			ALGORITHM = 'AEAD_AES_256_CBC_HMAC_SHA_256', 
			COLUMN_ENCRYPTION_KEY = Demo_Always_Encrypted_CEK
		),
	SSN CHAR(10) COLLATE Latin1_General_BIN2 
		ENCRYPTED WITH 
		(
		 ENCRYPTION_TYPE = DETERMINISTIC, 
		 ALGORITHM = 'AEAD_AES_256_CBC_HMAC_SHA_256', 
		 COLUMN_ENCRYPTION_KEY = Demo_Always_Encrypted_CEK
		) 
   );

---Insert a record into table. 
INSERT INTO dbo.Demo_Always_Encrypted  (LastName, FirstName, BirthDate, SSN)
VALUES ('Larsen','Gregory','1950-01-01', '123-45-6789');

-------------------------------------------------------------------------












